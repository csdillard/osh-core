import java.nio.file.*;

description = 'OSH Admin UI (Vaadin)'
ext.details = 'Web based admin user interface for OSH'

rootProject.allprojects {
  //ext.vaadinVersion = '7.7.12'
  ext.vaadinVersion = '8.12.4'
  repositories {
    maven { url "https://maven.vaadin.com/vaadin-addons" }
  }
}

dependencies {
  implementation project(':sensorhub-core')
  implementation project(':sensorhub-service-swe')
  embeddedImpl project(':sensorhub-webui-widgetset')
  embeddedImpl 'com.vaadin:vaadin-server:' + vaadinVersion
  embeddedImpl 'com.vaadin:vaadin-push:' + vaadinVersion  
  compileOnly 'com.vaadin:vaadin-client:' + vaadinVersion
  embeddedImpl 'com.vaadin:vaadin-client-compiled:' + vaadinVersion
  embeddedImpl 'com.vaadin:vaadin-compatibility-server:' + vaadinVersion
  compileOnly 'com.vaadin:vaadin-compatibility-client:' + vaadinVersion
  embeddedImpl 'com.vaadin:vaadin-compatibility-client-compiled:' + vaadinVersion
  embeddedImpl 'com.vaadin:vaadin-themes:' + vaadinVersion
  
  compileOnly 'org.osgi:org.osgi.core:5.0.0'
  compileOnly 'org.osgi:org.osgi.compendium:5.0.0'
  compileOnly 'org.apache.felix:org.apache.felix.bundlerepository:2.0.10'

  testImplementation project(path: ':sensorhub-core', configuration: 'testArtifacts')
}

// add info to OSGI manifest
osgi {
  manifest {
    attributes('Bundle-Vendor': 'Sensia Software LLC')
    attributes('Bundle-Activator': 'org.sensorhub.ui.Activator')
    
    // remove unused packages from imports
    attributes('Import-Package': '''
      !java.*,!org.w3c.dom.*,!org.xml.sax.*,!com.sun.*,
      !com.google.gwt.*,!com.google.debugging.*,!com.bea.*,!com.gargoylesoftware.*,
      !com.ibm.*,!com.liferay.*,!com.thoughtworks.*,!com.yahoo.*,!junit.*,!net.sourceforge.htmlunit.*,
      !org.apache.catalina.*,!org.apache.commons.*,!org.apache.coyote.*,!org.apache.shiro.*,
      !org.glassfish.grizzly.*,!org.atmosphere.*,!org.eclipse.jetty.websocket,!org.jboss.*,!org.json.*,
      !org.mortbay.*,!org.osgi.service.*,!org.testng.*,!weblogic.*,      
      !javax.annotation.*,!javax.enterprise.*,!javax.inject.*,!javax.jnlp.*,!javax.naming.*,
      !javax.net.*,!javax.portlet.*,!javax.sql.*,!javax.validation.*,!javax.websocket.*,!javax.xml.*,
      org.osgi.service.repository;resolution:=optional,
      *''')
    //!javax.(?!servlet).*
    
    /*attributes('Import-Package': '''
        org.osgi.framework,
        javax.servlet.*,
        org.sensorhub.*,
        org.vast.*,
        net.opengis.*,
        com.google.common.*,
        com.rits.*,
        com.vividsolutions.jts.*
    ''')*/
    
    attributes('-dsannotations-options': 'norequirements,nocapabilities')
    
    // fixup manifest to remove requirement introduced by vaadin osgi components
    doLast {
      def manifestEntry = 'META-INF/MANIFEST.MF'
      def newManifestFile = new File("$buildDir/MANIFEST.MF.fixed")
      
      // patch manifest directly in jar file
      Path jarFilePath = archivePath.toPath()
      FileSystems.newFileSystem(jarFilePath, null).withCloseable { fs ->
          def manifest = fs.getPath(manifestEntry)
          def newManifest = manifest.text.replaceAll("osgi.extender[\\s\\S]*?,", '')
          newManifestFile.text = newManifest
          manifest.text = newManifest
      }
    }
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'alexrobin'
      name 'Alex Robin'
      organization 'Sensia Software LLC'
      organizationUrl 'http://www.sensiasoftware.com' 
    }
  } 
}
